HOST_COLOR=${HOST_COLOR:-31}
USER_COLOR=${USER_COLOR:-24}
PROMPT_DIR_COLOR=${PROMPT_DIR_COLOR:-64}
VCS_FANCY_COLOR_1=${VCS_FANCY_COLOR_1:-$HOST_COLOR}
VCS_FANCY_COLOR_2=${VCS_FANCY_COLOR_2:-$USER_COLOR}

VCS_FANCY_PREFIX="%{%F{$VCS_FANCY_COLOR_1}%}∇%{%F{$VCS_FANCY_COLOR_2}%}┅❰ "
VCS_FANCY_SUFFIX=" %{%F{$VCS_FANCY_COLOR_2}%}❱┅%{%F{$VCS_FANCY_COLOR_1}%}┄"
VCS_BRANCH_COLOR=${VCS_BRANCH_COLOR:-242}
VCS_DESC_COLOR=${VCS_DESC_COLOR:-239}

autoload -U vcs_info
zstyle ':vcs_info:*' enable git hg bzr svn
zstyle ':vcs_info:(hg*|git*):*' check-for-changes true
zstyle ':vcs_info:*' formats "$VCS_FANCY_PREFIX%{%F{$VCS_BRANCH_COLOR}%}%b %{%F{$VCS_FANCY_COLOR_1}%}%c%u$VCS_FANCY_SUFFIX
"
zstyle ':vcs_info:*' branchformat '%b-%r'
zstyle ':vcs_info:git*:*' stagedstr "%{%F{2}%}A%{%}"
zstyle ':vcs_info:git*:*' unstagedstr "%{%F{1}%}M%{%}"
zstyle ':vcs_info:git+set-message:*' hooks git-check-untracked
# zstyle ':vcs_info:*+*:*' debug true

+vi-git-check-untracked() {
    local x
    [[ -n $(git ls-files --others --exclude-standard 2>&-) ]] && x=\?
    local desc
    desc="`git log -1 --pretty=format:'%s | %ar | %h' 2>&-`"
    hook_com[unstaged]="${hook_com[unstaged]}%{%F{1}%}$x %{%F{$VCS_DESC_COLOR}%}$desc"
}

precmd() vcs_info
setopt promptsubst
export PROMPT="\${vcs_info_msg_0_}%{%F{$HOST_COLOR}%}%B%#%b%{$reset_color%} "
export RPROMPT=\
"%{%F{$USER_COLOR}%}%n%{${reset_color}%}@%{%F{$HOST_COLOR}%}%m%b:%{%F{$PROMPT_DIR_COLOR}%}%B%~"
# vim:ft=zsh
